      SUBROUTINE READCNTRL(IEOF)
C
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .     
C SUBPROGRAM:    READCNTRL  READS CONTROL FILE
C   PRGRMMR: TREADON         ORG: W/NP2      DATE: 92-12-20       
C     
C ABSTRACT:
C     THIS ROUTINE READS THE CONTROL FILE SPECIFYING
C     DATA FORMAT(S) AND FIELD(S) TO POST.  THE
C     ORDER OF OPERATIONS IS 
C        (1) READ HEADER BLOCK OF CONTROL FILE,
C        (2) SET FLAGS, CLOSE OPEN UNITS
C        (3) READ BODY OF CONTROL FILE (FIELD SPECIFICATIONS)
C   .     
C     
C PROGRAM HISTORY LOG:
C   92-12-20  RUSS TREADON
C   93-06-15  RUSS TREADON - ADD PROJECTION CONTROL CARD
C   98-06-01  BLACK - CONVERSION OF POST FROM 1-D TO 2-D
C   98-07-17  MIKE BALDWIN - REMOVED PACK84
C   01-10-22  H CHUANG - MODIFIED TO PROCESS HYBRID MODEL OUTPUT
C   02-01-16  MIKE BALDWIN - WRF VERSION
C     
C USAGE:    CALL READCNTRL(IEOF)
C   INPUT ARGUMENT LIST:
C     NONE
C
C   OUTPUT ARGUMENT LIST: 
C     IEOF     - INTEGER FLAG FOR EOF IN CONTROL FILE.
C                IEOF=0 WHEN AN EOF IS READ IN THE
C                CONTROL FILE.  IEOF=1 OTHERWISE.
C     
C   OUTPUT FILES:
C     NONE
C     
C   SUBPROGRAMS CALLED:
C     UTILITIES:
C
C     LIBRARY:
C       COMMON   - RQSTFLD 
C                  CTLBLK
C     
C   ATTRIBUTES:
C     LANGUAGE: FORTRAN
C     MACHINE : CRAY C-90
C$$$  
C
C     
C     INCLUDE ETA GRID DIMENSIONS.  SET/DERIVE PARAMETERS.
C
!      INCLUDE "parmeta"
!      INCLUDE "parmout"
      INCLUDE "parm.tbl"
!      PARAMETER (IMJM=IM*JM-JM/2,IMT=2*IM-1,JMT=JM,LP1=LM+1)
      PARAMETER (DTR=1.745329E-2,RTD=1./DTR)
C
C     INCLUDE COMMON BLOCKS.
      INCLUDE "RQSTFLD.comm"
      INCLUDE "CTLBLK.comm"
C     
C     DECLARE VARIABLES.
C     
      LOGICAL NORTH
      CHARACTER*2  CHAR2
      CHARACTER*4  CHAR4
      CHARACTER*80 LINE
      REAL EGRID1(IM,JM), EGRID2(IM,JM)
C
C******************************************************************************
C     START READCNTRL HERE.
C     

      LCNTRL=14
      LUNOUT=60

      IF(ME.EQ.0)THEN
        WRITE(6,*)'READCNTRL:  POSTING FCST HR ',IFHR,' FROM ',
     X       IHRST,'UTC ',SDAT(1),'-',SDAT(2),'-',SDAT(3),' RUN'
      ENDIF
C     
C     INITIALIZE VARIABLES.
C        IEOF IS THE END OF FILE FLAG FOR THE CONTROL FILE.
C        ARRAY IGET IS THE "GET FIELD" FLAG ARRAY.
C
      IEOF=0
      DO 100 IFLD=1,MXFLD
        IGET(IFLD)=-1
 100  CONTINUE
C
      if(me.eq.0)print*,'start reading control file'
C
      READ(LCNTRL,1000,ERR=990,END=999) KGTYPE
      READ(LCNTRL,1000,ERR=990,END=999) IMDLTY
      READ(LCNTRL,1030,ERR=990,END=999) DATSET
 1000 FORMAT(T28,I5)
 1030 FORMAT(T28,A6)
C     
C     SET FLAG TO OPEN NEW OUTPUT FILE
C
      RITEHD = .TRUE.
C     
C     ECHO HEADER INFO TO 6.
C
      IF(ME.EQ.0)THEN
        WRITE(6,*)'READCNTRL:  HEADER INFORMATION'
        WRITE(6,*)' KGTYPE       :  ',KGTYPE
        WRITE(6,*)' IMDLTY       :  ',IMDLTY
        WRITE(6,*)' DATSET       :  ',DATSET
        WRITE(6,*)' RITEHD       :  ',RITEHD
      ENDIF
C     
C     NOW READ WHICH FIELDS ON 
C     WHICH LEVELS TO INTERPOLATE TO THE OUTPUT GRID.  THE
C     CHARACTER STRING "DONE" MARKS THE END OF THE OUTPUT
C     FIELD SPECIFICATIONS.
C
      IFLD = 0
 10   CONTINUE
         READ(LCNTRL,1060,ERR=996) LINE
         IF (INDEX(LINE,'DONE').NE.0) GOTO 40
         IF (INDEX(LINE,'SCAL=').EQ.0)   GOTO 10
         IFLD        = IFLD+1
         FIELD(IFLD) = LINE(3:22)
         READ(LINE,1061) DEC(IFLD)
         READ(LCNTRL,1090,ERR=996) (LVLS(L,IFLD),L=1,MXLVL)
 1060    FORMAT(A80)
 1061    FORMAT(30X,F4.1)
 1070    FORMAT(A4)
 1080    FORMAT(A2)
 1090    FORMAT(T5,14(5I1,1X))
C     
C        SEE IF WE WANT THIS FIELD.  THE SUM OF THE LEVELS
C        INDICATORS MUST BE GREATER THAN ZERO IF WE WANT 
C        THIS FIELD.
C     
         ISUM = 0
         DO 15 L = 1,MXLVL
            ISUM = ISUM + LVLS(L,IFLD)
 15      CONTINUE
         IF (ISUM.LT.1) THEN
            IFLD = IFLD - 1
            GOTO 10
         ENDIF
C     
C        SEE IF REQUESTED FIELD IS AVAILABLE.  IF NOT, 
C        WRITE MESSAGE TO 6 AND DECREMENT FIELD 
C        COUNTER BY ONE.  THEN READ NEXT REQUESTED FIELD.
C     
         DO 20 IAVBL = 1,MXFLD
            IF (INDEX(FIELD(IFLD),AVBL(IAVBL)).NE.0)GO TO 30
 20      CONTINUE
         IF(ME.EQ.0)THEN
           WRITE(6,*)'FIELD ',FIELD(IFLD),' NOT AVAILABLE'
         ENDIF
         IFLD = IFLD-1
         GOTO 10
C     
C        IF FIELD IS AVAILABLE, TURN THE GET SWITCH ON.
C     
 30      CONTINUE
         IGET(IAVBL) = IFLD
         IDENT(IFLD) = IAVBL
         GOTO 10
C     
C     ALL DONE READING REQUESTED FIELDS FOR CURRENT OUTPUT GRID.
C     SET NFLD TO TOTAL NUMBER OF REQUESTED OUTPUT FIELDS THAT 
C     ARE AVAILABLE.
C
 40   CONTINUE
      NFLD = IFLD
C     
C     ECHO OUTPUT FIELDS/LEVELS TO 6.
C
      IF(ME.EQ.0)THEN
        WRITE(6,*)'BELOW ARE FIELD/LEVEL/SMOOTHING ',
     X       'SPECIFICATIONS.'
      ENDIF
      DO 50 IFLD = 1,NFLD
        IF(ME.EQ.0)THEN
         WRITE(6,2060) FIELD(IFLD)
         WRITE(6,2070) (LVLS(L,IFLD),L=1,MXLVL)
 2060    FORMAT('(',A20,')')
 2070    FORMAT('L=(',14(5I1,1X),')')
        ENDIF
 50   CONTINUE
C     
C     WE HAVE AN OUTPUT GRID AND THE FIELDS TO GENERATE ON IT.
C     SKIP OVER THE FOLLOWING EOF MESSAGE TO EXIT THIS ROUTINE.
C     
      GOTO 60
C     
C     WE REACH THIS BLOCK ONLY IF THERE IS AN ERROR WHILE READING
C     IN THE CONTROL FILE.  PRINT AN ERROR MESSAGE TO STANDARD
C     OUT AND CARRY ON.
C     
 990  CONTINUE
      IF(ME.EQ.0)THEN
        WRITE(6,*)' READCNTRL:  ERROR READING CNTRL HEADER INFO'
        WRITE(6,*)' BELOW IS CNTRL GRID INFO'
        WRITE(6,*)'  KGTYPE,DATSET:  ',KGTYPE,' ',DATSET
      ENDIF
      GOTO 999
 996  CONTINUE
      IF(ME.EQ.0)THEN
        WRITE(6,*)' READCNTRL:  ERROR READING CNTRL FLD/LVL INFO'
      ENDIF
C     
C     WE REACH THIS BLOCK ONLY WHEN AN EOF HAS BEEN READ FROM 
C     THE CONTROL FILE.  THAT MEANS WE'VE PROCESSED ALL GRIDS
C     AND ALL FIELDS.  WE'RE DONE.  SET THE EOF FLAG TO ANY
C     NONZERO INTEGER, SAY ONE.  CLOSE THE UNIT CONNECTED TO
C     THE LAST OUTPUT FILE AND EXIT THE ROUTINE.
C     
 999  CONTINUE
      IEOF=1
      CLOSE(LUNOUT)
      IF(ME.EQ.0)THEN
        WRITE(6,*)' READCNTRL:  ALL GRIDS PROCESSED.  ',
     X       'CLOSED ',LUNOUT
      ENDIF
C     
C     END OF ROUTINE.
C     
 60   CONTINUE
      RETURN
      END
