      SUBROUTINE CALRH(P1,T1,Q1,RH)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .     
C SUBPROGRAM:    CALRH       COMPUTES RELATIVE HUMIDITY
C   PRGRMMR: TREADON         ORG: W/NP2      DATE: 92-12-22       
C     
C ABSTRACT:  
C     THIS ROUTINE COMPUTES RELATIVE HUMIDITY GIVEN PRESSURE, 
C     TEMPERATURE, SPECIFIC HUMIDITY. AN UPPER AND LOWER BOUND
C     OF 100 AND 1 PERCENT RELATIVE HUMIDITY IS ENFORCED.  WHEN
C     THESE BOUNDS ARE APPLIED THE PASSED SPECIFIC HUMIDITY 
C     ARRAY IS ADJUSTED AS NECESSARY TO PRODUCE THE SET RELATIVE
C     HUMIDITY.
C   .     
C     
C PROGRAM HISTORY LOG:
C   ??-??-??  DENNIS DEAVEN
C   92-12-22  RUSS TREADON - MODIFIED AS DESCRIBED ABOVE.
C   98-06-08  T BLACK      - CONVERSION FROM 1-D TO 2-D
C   98-08-18  MIKE BALDWIN - MODIFY TO COMPUTE RH OVER ICE AS IN MODEL
C   98-12-16  GEOFF MANIKIN - UNDO RH COMPUTATION OVER ICE
C   00-01-04  JIM TUCCILLO - MPI VERSION
C   02-06-11  MIKE BALDWIN - WRF VERSION
C     
C USAGE:    CALL CALRH(P1,T1,Q1,RH)
C   INPUT ARGUMENT LIST:
C     P1     - PRESSURE (PA)
C     T1     - TEMPERATURE (K)
C     Q1     - SPECIFIC HUMIDITY (KG/KG)
C
C   OUTPUT ARGUMENT LIST: 
C     RH     - RELATIVE HUMIDITY  (DECIMAL FORM)
C     Q1     - ADJUSTED SPECIFIC HUMIDITY (KG/KG)
C     
C   OUTPUT FILES:
C     NONE
C     
C   SUBPROGRAMS CALLED:
C     UTILITIES:
C     LIBRARY:
C       NONE
C     
C   ATTRIBUTES:
C     LANGUAGE: FORTRAN
C     MACHINE : CRAY C-90
C$$$  
C
!      INCLUDE "parmeta"
      INCLUDE "params"
      INCLUDE "CTLBLK.comm"
C     
C     SET PARAMETER.
C
!      PARAMETER (A2=17.2693882,A3=273.16,A4=35.86,
!     &  PQ0=379.90516)
C
C     DECLARE VARIABLES.
C     
      REAL QC,P1(IM,JM),T1(IM,JM),Q1(IM,JM),RH(IM,JM) 
C***************************************************************
C
C     START CALRH.
C
      DO J=JSTA,JEND
        DO I=1,IM
        IF (T1(I,J).LT.SPVAL) THEN
         IF (ABS(P1(I,J)).GT.1) THEN
           QC=PQ0/P1(I,J)
     1          *EXP(A2*(T1(I,J)-A3)/(T1(I,J)-A4))
C
C
           RH(I,J)=Q1(I,J)/QC

C
C   BOUNDS CHECK
C
           IF (RH(I,J).GT.1.0) THEN
            RH(I,J)=1.0
            Q1(I,J)=RH(I,J)*QC
           ENDIF
!           IF (RH(I,J).LT.0.01) THEN
           IF (RH(I,J).LT.RHmin) THEN  !use smaller RH limit for stratosphere
            RH(I,J)=RHmin
            Q1(I,J)=RH(I,J)*QC
           ENDIF
C
         ENDIF
        ELSE
         RH(I,J)=SPVAL
        ENDIF
        ENDDO
      ENDDO

      RETURN
      END
