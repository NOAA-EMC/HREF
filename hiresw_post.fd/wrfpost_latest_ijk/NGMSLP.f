      SUBROUTINE NGMSLP
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .     
C SUBPROGRAM:    NGMSLP      NMC SEA LEVEL PRESSURE REDUCTION
C   PRGRMMR: TREADON         ORG: W/NP2      DATE: 93-02-02       
C     
C ABSTRACT:
C     
C     THIS ROUTINE COMPUTES SEA LEVEL PRESSURE USING THE
C     HYDROSTATIC EQUATION WITH THE SHUELL CORRECTION.  THE
C     FOLLOWING IS BASED ON DOCUMENTATION IN SUBROUTINE 
C     OUTHYDRO OF THE NGM:
C     
C     THE FUNDAMENTAL HYDROSTATIC EQUATION IS
C        D(HEIGHT)
C        ---------  =  TAU = VIRTUAL TEMPERATURE * (RGAS/GRAVITY)
C        D (Z)
C      WHERE
C        Z = MINUS LOG OF PRESSURE (-LN(P)).
C
C     SEA-LEVEL PRESSURE IS COMPUTED FROM THE FORMULA
C        PRESS(MSL) = PRESS(GROUND) * EXP( F)
C     WHERE
C        F        = HEIGHT OF GROUND / MEAN TAU
C        MEAN TAU = ( TAU(GRND) + TAU(SL) ) / 2
C     
C     IN THE NGM TAU(GRND) AND TAU(SL) ARE FIRST SET USING A 
C     6.5DEG/KM LAPSE RATE FROM LOWEST MDL LEVEL.  THIS IS MODIFIED
C     BY A CORRECTION BASED ON THE CRITICAL TAU OF THE SHUELL
C     CORRECTION:
C                  TAUCR=(RGASD/GRAVITY) * 290.66
C   
C     1) WHERE ONLY TAU(SL) EXCEEDS TAUCR, CHANGE TAU(SL) TO TAUCR.
C
C     2) WHERE BOTH TAU(SL) AND TAU(GRND) EXCEED TAUCR,
C        CHANGE TAU(SL) TO TAUCR-CONST*(TAU(GRND)-TAUCR  )**2
C        WHERE CONST = .005 (GRAVITY/RGASD)
C   
C     THE AVERAGE OF TAU(SL) AND TAU(GRND) IS THEN USED TOGETHER
C     WITH THE GROUND HEIGHT AND PRESSURE TO DERIVE THE PRESSURE
C     AT SEA LEVEL. 
C     
C     HEIGHT OF THE 1000MB SURFACE IS COMPUTED FROM THE MSL PRESSURE
C     FIELD USING THE FORMULA:
C     
C       P(MSL) - P(1000MB) = MEAN DENSITY * GRAVITY * HGT(1000MBS)
C     
C     WHERE P(MSL) IS THE SEA LEVEL PRESSURE FIELD WE HAVE JUST
C     COMPUTED.
C     
C
C     MEB 6/13/02: THIS CODE HAS BEEN SIMPLIFIED CONSIDERABLY FROM
C     THE ONE USED IN ETAPOST.  HORIZONTAL SMOOTHING HAS BEEN
C     REMOVED AND THE FIRST MODEL LEVEL IS USED RATHER
C     THAN THE MEAN OF THE VIRTUAL TEMPERATURES IN
C     THE LOWEST 30MB ABOVE GROUND TO COMPUTE TAU(GRND).
C     
C   .     
C     
C PROGRAM HISTORY LOG:
C   93-02-02  RUSS TREADON
C   98-06-08  T BLACK - CONVERSION FROM 1-D TO 2-D
C   00-01-04  JIM TUCCILLO - MPI VERSION
C   01-10-25  H CHUANG - MODIFIED TO PROCESS HYBRID MODEL OUTPUT
C   01-11-02  H CHUANG - MODIFIED LINE 234 FOR COMPUTATION OF 
C                         SIGMA/HYBRID SLP
C   01-12-18  H CHUANG - INCLUDED SMOOTHING ALONG BOUNDARIES TO BE
C                         CONSISTENT WITH MESINGER SLP
C   02-06-13  MIKE BALDWIN - WRF VERSION
C     
C USAGE:    CALL NGMSLP
C   INPUT ARGUMENT LIST:
C     NONE     
C
C   OUTPUT ARGUMENT LIST: 
C     NONE
C     
C   OUTPUT FILES:
C     NONE
C     
C   SUBPROGRAMS CALLED:
C     UTILITIES:
C       NONE
C     LIBRARY:
C       COMMON   - CTLBLK
C     
C   ATTRIBUTES:
C     LANGUAGE: FORTRAN
C     MACHINE : CRAY C-90
C$$$  
C     
      use vrbls3d
      use vrbls2d
      use masks
C     
C     INCLUDE PARAMETERS.  SET LOCAL PARAMETERS.
!      INCLUDE "parmeta"
      INCLUDE "params"
C
C     INCLUDE COMMON BLOCKS
      INCLUDE "CTLBLK.comm"
C
      PARAMETER (GAMMA=6.5/1000.,ZSL=0.0)
      PARAMETER (TAUCR=RD*GI*290.66,CONST=0.005*G/RD)
      PARAMETER (GORD=G/RD,DP=60.E2)
C     
C     DECLARE VARIABLES
      REAL TBAR(IM,JM),QBAR(IM,JM),ALPBAR(IM,JM)
      REAL TSFC(IM,JM),QSFC(IM,JM),EGRID(IM,JM)
C     
C**********************************************************************
C     START NGMSLP HERE.
C     
C     LOOP OVER HORIZONTAL GRID.
C
!$omp  parallel do
!$omp& private(llmh,pavg,psfc,qavg,rhoavg,rrhog,
!$omp&         tau,tauavg,tausfc,tausl,tavg,tvrbar,tvrsfc,tvrsl,
!$omp&         tvrt,tvrtal,zbar,zl,zsfc)
       DO J=JSTA,JEND
       DO I=1,IM
         LLMH = NINT(LMH(I,J))
         ZSFC = ZINT(I,J,LLMH+1)
         PSFC = PINT(I,J,LLMH+1)
         SLP(I,J) = PSFC
C
C        COMPUTE LAYER TAU (VIRTUAL TEMP*RD/G).
         TVRT = T(I,J,LLMH)*(H1+D608*Q(I,J,LLMH))
         TAU  = TVRT*RD*GI
C     
C        COMPUTE TAU AT THE GROUND (Z=ZSFC) AND SEA LEVEL (Z=0)
C        ASSUMING A CONSTANT LAPSE RATE OF GAMMA=6.5DEG/KM.
         TVRSFC = TVRT + (ZSFC- ZSL)*GAMMA
         TAUSFC = TVRSFC*RD*GI
         TVRSL  = TVRT + (ZSFC- ZSL)*GAMMA
         TAUSL  = TVRSL*RD*GI
C     
C        IF NEED BE APPLY SHEULL CORRECTION.
         IF ((TAUSL.GT.TAUCR).AND.(TAUSFC.LE.TAUCR)) THEN
            TAUSL=TAUCR
         ELSEIF ((TAUSL.GT.TAUCR).AND.(TAUSFC.GT.TAUCR)) THEN
            TAUSL = TAUCR-CONST*(TAUSFC-TAUCR)**2
         ENDIF
C     
C        COMPUTE MEAN TAU.
         TAUAVG = D50*(TAUSL+TAUSFC)
C     
C        COMPUTE SEA LEVEL PRESSURE.
         IF (FIS(I,J).GT.1.0)SLP(I,J) = PSFC*EXP(ZSFC/TAUAVG)
C     
C        COMPUTE 1000MB HEIGHTS.
         ALPAVG   = D50*(ALOG(PSFC)+ALOG(SLP(I,J)))
         PAVG     = EXP(ALPAVG)
         RHOAVG   = PAVG*GI/TAUAVG
         RRHOG    = H1/(RHOAVG*G)
         Z1000(I,J) = (SLP(I,J)-P1000)*RRHOG
C     
C     MOVE TO NEXT HORIZONTAL GRIDPOINT.
      ENDDO
      ENDDO
C     
C     
C     END OF ROUTINE.
C     

      RETURN
      END
