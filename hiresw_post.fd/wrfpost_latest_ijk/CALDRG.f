      SUBROUTINE CALDRG(DRAGCO)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .     
C SUBPROGRAM:    CALDRG      COMPUTE DRAG COEFFICIENT
C   PRGRMMR: TREADON         ORG: W/NP2      DATE: 93-09-01
C     
C ABSTRACT:  THIS ROUTINE COMPUTES A SURFACE LAYER DRAG
C   COEFFICIENT USING EQUATION (7.4.1A) IN "AN INTRODUCTION
C   TO BOUNDARY LAYER METEOROLOGY" BY STULL (1988, KLUWER
C   ACADEMIC PUBLISHERS).
C   .     
C     
C PROGRAM HISTORY LOG:
C   93-09-01  RUSS TREADON
C   98-06-15  T BLACK - CONVERSION FROM 1-D TO 2-D
C   00-01-04  JIM TUCCILLO - MPI VERSION           
C   02-01-15  MIKE BALDWIN - WRF VERSION
C   05-02-22 H CHUANG - ADD WRF NMM COMPONENTS 
C     
C USAGE:    CALL CALDRG(DRAGCO)
C   INPUT ARGUMENT LIST:
C     NONE     
C
C   OUTPUT ARGUMENT LIST: 
C     DRAGCO   - SURFACE LAYER DRAG COEFFICIENT
C     
C   OUTPUT FILES:
C     NONE
C     
C   SUBPROGRAMS CALLED:
C     UTILITIES:
C       NONE
C     LIBRARY:
C       COMMON   - LOOPS
C                  SRFDSP
C                  PVRBLS
C     
C   ATTRIBUTES:
C     LANGUAGE: FORTRAN 90
C     MACHINE : CRAY C-90
C$$$  
C     
C
      use vrbls3d
      use vrbls2d
      use masks
C
C     INCLUDE/SET PARAMETERS.
C     
!      INCLUDE "parmeta"
      INCLUDE "params"
C     INCLUDE COMMON BLOCKS.
C
      INCLUDE "CTLBLK.comm"
C     
C     DECLARE VARIABLES.
      REAL DRAGCO(IM,JM)
      INTEGER IHE(JM),IHW(JM)
C     
C     
C     
C********************************************************************
C     START CALDRG HERE.
C     
C     INITIALIZE DRAG COEFFICIENT ARRAY TO ZERO.
C     
      DO J=JSTA,JEND
      DO I=1,IM
        DRAGCO(I,J) = D00
      ENDDO
      ENDDO
C

      IF(MODELNAME .EQ. 'NCAR')THEN 
       DO J=JSTA,JEND
       DO I=1,IM
C     
        LMHK=NINT(LMH(I,J))
C     
C        COMPUTE A MEAN MASS POINT WIND SPEED BETWEEN THE
C        FIRST ATMOSPHERIC ETA LAYER AND Z0.  ACCORDING TO
C        NETCDF OUTPUT, UZ0 AND VZ0 ARE AT MASS POINTS. (MEB 6/11/02)
C
        UBAR=D50*(UH(I,J,LMHK)+UZ0(I,J))
        VBAR=D50*(VH(I,J,LMHK)+VZ0(I,J))
        WSPDSQ=UBAR*UBAR+VBAR*VBAR
C     
C        COMPUTE A DRAG COEFFICIENT.
C
        USTRSQ=USTAR(I,J)*USTAR(I,J)
        IF(WSPDSQ .GT. 1.0E-6)DRAGCO(I,J)=USTRSQ/WSPDSQ
C
       ENDDO
       ENDDO
      ELSE IF(MODELNAME .EQ. 'NMM')THEN
      
       DO J=JSTA_M,JEND_M
        IHE(J)=MOD(J+1,2)
        IHW(J)=IHE(J)-1
       ENDDO
       
       DO J=JSTA_M,JEND_M
       DO I=2,IM-1
C
C        COMPUTE A MEAN MASS POINT WIND IN THE
C        FIRST ATMOSPHERIC ETA LAYER.
C
        LMHK=NINT(LMH(I,J))
        IE=I+IHE(J)
        IW=I+IHW(J)
        SUMU=U(IE,J,LMHK)+U(IW,J,LMHK)+U(I,J-1,LMHK)
     1    +U(I,J+1,LMHK)
        SUMV=V(IE,J,LMHK)+V(IW,J,LMHK)+V(I,J-1,LMHK)
     1    +V(I,J+1,LMHK)
        ULMH=D25*SUMU
        VLMH=D25*SUMV
C
C        COMPUTE A MEAN MASS POINT WIND AT HEIGHT Z0.
C
        UZ0H=D25*(UZ0(IE,J)+UZ0(IW,J)+UZ0(I,J-1)+UZ0(I,J+1))
        VZ0H=D25*(VZ0(IE,J)+VZ0(IW,J)+VZ0(I,J-1)+VZ0(I,J+1))
C
C        COMPUTE A MEAN MASS POINT WIND SPEED BETWEEN THE
C        FIRST ATMOSPHERIC ETA LAYER AND Z0.
C
        UBAR=D50*(ULMH+UZ0H)
        VBAR=D50*(VLMH+VZ0H)
        WSPDSQ=UBAR*UBAR+VBAR*VBAR
cjjt  WSPDSQ=MIN(WSPDSQ,0.1)
C
C        COMPUTE A DRAG COEFFICIENT.
C
        USTRSQ=USTAR(I,J)*USTAR(I,J)
        IF(WSPDSQ .GT. 1.0E-6)DRAGCO(I,J)=USTRSQ/WSPDSQ
C
       END DO
       END DO
      ELSE 
      
       DO J=JSTA,JEND
       DO I=1,IM
        DRAGCO(I,J) = SPVAL
       ENDDO
       ENDDO

      END IF 
C     
C     END OF ROUTINE.
C     
      RETURN
      END
