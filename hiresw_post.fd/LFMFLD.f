      SUBROUTINE LFMFLD(RH3310,RH6610,RH3366,PW3310)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .     
C SUBPROGRAM:    LFMFLD      COMPUTES LAYER MEAN LFM FIELDS
C   PRGRMMR: TREADON         ORG: W/NP2      DATE: 92-12-22
C     
C ABSTRACT:
C     THIS ROUTINE COMPUTES THREE LAYER MEAN RELATIVE HUMIDITIES
C     AND A PRECIPITABLE WATER FIELD FROM ETA LEVEL DATA.  THE
C     COMPUTED FIELDS ARE INTENDED TO MIMIC SIMILAR FIELDS COM-
C     PUTED BY THE LFM.  THE ALGORITHM USED HERE IS FAIRLY PRI-
C     MATIVE.  IN EACH COLUMN ABOVE A MASS POINT ON THE ETA GRID
C     WE SET THE FOLLOWING TARGET PRESSURES:
C         SIGMA LAYER 1.00 PRESSURE:  SURFACE PRESSURE
C         SIGMA LAYER 0.66 PRESSURE:  0.50 * SURFACE PRESSURE
C         SIGMA LAYER 0.33 PRESSURE:  0.4356 * SURFACE PRESSURE
C     GIVEN THESE PRESSURES A SURFACE UP SUMMATION IS MADE OF 
C     RELATIVE HUMIDITY AND/OR PRECIPITABLE WATER BETWEEN THESE
C     TARGET PRESSURES.  EACH TERM IN THE SUMMATION IS WEIGHTED
C     BY THE THICKNESS OF THE ETA LAYER.  THE FINAL LAYER MEAN
C     IS THIS SUM NORMALIZED BY THE TOTAL DEPTH OF THE LAYER.  
C     THERE IS, OBVIOUSLY, NO NORMALIZATION FOR PRECIPITABLE WATER.
C
C     
C PROGRAM HISTORY LOG:
C   92-12-22  RUSS TREADON
C   93-07-27  RUSS TREADON - MODIFIED SUMMATION LIMITS FROM
C                            0.66*PSFC TO 0.75*PSFC AND 0.33*PSFC 
C                            TO 0.50*PSFC, WHERE PSFC IS THE
C                            SURFACES PRESSURE.  THE REASON FOR
C                            THIS CHANGE WAS RECOGNITION THAT IN 
C                            THE LFM 0.33 AND 0.66 WERE MEASURED
C                            FROM THE SURFACE TO THE TROPOPAUSE,
C                            NOT THE TOP OF THE MODEL.
C   93-09-13  RUSS TREADON - RH CALCULATIONS WERE MADE INTERNAL
C                            TO THE ROUTINE.
C   96-03-04  MIKE BALDWIN - CHANGE PW CALC TO INCLUDE CLD WTR 
C   98-06-16  T BLACK      - CONVERSION FROM 1-D TO 2-D
C   98-08-17  MIKE BALDWIN - COMPUTE RH OVER ICE
C   98-12-22  MIKE BALDWIN - BACK OUT RH OVER ICE
C   00-01-04  JIM TUCCILLO - MPI VERSION
C   02-04-24  MIKE BALDWIN - WRF VERSION
C     
C     
C USAGE:    CALL LFMFLD(RH3310,RH6610,RH3366,PW3310)
C   INPUT ARGUMENT LIST:
C     NONE
C
C   OUTPUT ARGUMENT LIST: 
C     RH3310   - SIGMA LAYER 0.33-1.00 MEAN RELATIVE HUMIDITY.
C     RH6610   - SIGMA LAYER 0.66-1.00 MEAN RELATIVE HUMIDITY.
C     RH3366   - SIGMA LAYER 0.33-0.66 MEAN RELATIVE HUMIDITY.
C     PW3310   - SIGMA LAYER 0.33-1.00 PRECIPITABLE WATER.
C     
C   OUTPUT FILES:
C     NONE
C     
C   LIBRARY:
C     COMMON   - 
C                MAPOT
C                LOOPS
C                OPTIONS
C
C   ATTRIBUTES:
C     LANGUAGE: FORTRAN
C     MACHINE : CRAY C-90
C$$$  
C     
C
      use vrbls3d
      use masks
C     
C     INCLUDE PARAMETERS.
!      INCLUDE "parmeta"
      INCLUDE "params"
C
C     INCLUDE COMMON BLOCKS.
      INCLUDE "CTLBLK.comm"
C
      PARAMETER (RHOWAT=1.E3)
C     
C     DECLARE VARIABLES.
C     
      REAL ALPM, DZ, ES, PM, PWSUM, QM, QS, TM
      REAL RH3310(IM,JM), RH6610(IM,JM), RH3366(IM,JM)
      REAL PW3310(IM,JM)
C
C***********************************************************************
C     START LFMFLD HERE
C     
C
C     LOOP OVER HORIZONTAL GRID.
C     
      DO 30 J=JSTA,JEND
      DO 30 I=1,IM
C     
C        ZERO VARIABLES.
         RH3310(I,J) = D00
         PW3310(I,J) = D00
         RH6610(I,J) = D00
         RH3366(I,J) = D00
         Z3310     = D00
         Z6610     = D00
         Z3366     = D00
C     
C        SET BOUNDS FOR PRESSURES AND SURFACE L.
         P10  = PINT(I,J,NINT(LMH(I,J)))
         P66  = 0.75*P10
         P33  = 0.50*P10
         LLMH = NINT(LMH(I,J))
C     
C        ACCULMULATE RELATIVE HUMIDITIES AND PRECIPITABLE WATER.
C
         DO 10 L = LLMH,1,-1
C     
C           GET P, Z, T, AND Q AT MIDPOINT OF ETA LAYER.
            ALPM = D50*(ALPINT(I,J,L)+ALPINT(I,J,L+1))
            DZ   = ZINT(I,J,L)-ZINT(I,J,L+1)
            DP   = PINT(I,J,L+1)-PINT(I,J,L)
            PM   = EXP(ALPM)
            TM   = T(I,J,L)
            QM   = Q(I,J,L)
            QM   = AMAX1(QM,D00)
C
            QS=PQ0/PM*EXP(A2*(TM-A3)/(TM-A4))
            RH   = QM/QS
            IF (RH.GT.H1) THEN
               RH = H1
               QM = RH*QS
            ENDIF
            IF (RH.LT.D01) THEN
               RH = D01
               QM = RH*QS
            ENDIF
C
C           JUMP OUT OF THIS LOOP IF WE ARE ABOVE THE HIGHEST TARGET PRESSURE.
            IF (PM.LE.P33) GOTO 20
C     
C           0.66-1.00 RELATIVE HUMIDITY.
            IF ((PM.LE.P10).AND.(PM.GE.P66)) THEN
               Z6610     = Z6610 + DZ
               RH6610(I,J) = RH6610(I,J) + RH*DZ
            ENDIF
C     
C           0.33-1.00 RELATIVE HUMIDITY AND PRECIPITABLE WATER.
            IF ((PM.LE.P10).AND.(PM.GE.P33)) THEN
               Z3310      = Z3310 + DZ
               RH3310(I,J)= RH3310(I,J)+RH*DZ
               PW3310(I,J)= PW3310(I,J)+(Q(I,J,L)+CWM(I,J,L))*DP*GI
            ENDIF
C     
C           0.33-0.66 RELATIVE HUMIDITY.
            IF ((PM.LE.P66).AND.(PM.GE.P33)) THEN
               Z3366     = Z3366 + DZ
               RH3366(I,J) = RH3366(I,J) + RH*DZ
            ENDIF
C
 10      CONTINUE
 20      CONTINUE
C     
C        NORMALIZE TO GET MEAN RELATIVE HUMIDITIES.  AT
C        ONE TIME WE DIVIDED PRECIPITABLE WATER BY DENSITY
C        TO GET THE EQUIVALENT WATER DEPTH IN METERS.  NO MORE.
         IF (Z6610.GT.D00) THEN
            RH6610(I,J) = RH6610(I,J)/Z6610
         ELSE
            RH6610(I,J) = SPVAL
         ENDIF
C     
         IF (Z3310.GT.D00) THEN
            RH3310(I,J) = RH3310(I,J)/Z3310
         ELSE
            RH3310(I,J) = SPVAL
         ENDIF
C     
         IF (Z3366.GT.D00) THEN
            RH3366(I,J) = RH3366(I,J)/Z3366
         ELSE
            RH3366(I,J) = SPVAL
         ENDIF
 30   CONTINUE
C     
C     
C     END OF ROUTINE.
C     
      RETURN
      END
